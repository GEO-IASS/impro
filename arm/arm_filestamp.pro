;+
; NAME: ARM_FILESTAMP
;       
; PURPOSE: stamps file header with time, program of origin, etc.
;
; CATEGORY: miscellaneous
;
; CALLING SEQUENCE: ARM_FILESTAMP, fnum, [comment=, notes=]
;
; INPUTS: fnum - file number to stamp
;       
; OPTIONAL INPUTS: comment - text to precede each line
;                  notes   - lines of additional text (string array)
;
; EXAMPLE: ARM_FILESTAMP, 2, comment='%', notes=['line 1', 'line 2']
;
; COMMENTS: stamp will only make sense if this routine is called
;           within the same routine that creates the stamped file
;
; MODIFICATION HISTORY:
;    written by A.R.Marble, Steward Obs., June 2003
;-

pro ARM_FILESTAMP, fnum, comment=comment, notes=notes

; define default comment character

    if not KEYWORD_SET(comment) then comment = '#'
    
; determine the name of the calling routine

    HELP, calls=calls
    fname = (STR_SEP(calls[1], ' '))[0]

; output time and program of origin

    PRINTF, fnum, comment+' GENERATED BY IDL FILE: '+fname+' ('+SYSTIME()+')'
    PRINTF, fnum, comment

; output additional lines of text if applicable

    if KEYWORD_SET(notes) then begin

       for i=0,N_ELEMENTS(notes)-1 do PRINTF, fnum, comment+' '+notes[i]
       PRINTF, fnum, comment

    endif

    return

 end


